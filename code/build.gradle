plugins {
    id 'com.enonic.xp.app' version '3.1.0'
    id "com.github.node-gradle.node" version "3.3.0"
    id 'org.ajoberstar.grgit' version '4.1.1'
}

app {
    name = project.ext.appName
    displayName = project.ext.displayName
    vendorName = project.ext.vendorName
    vendorUrl = project.ext.vendorUrl
    systemVersion = "${xpVersion}"
    keepArchiveFileName = true
}

ext {
    deployDir = "../deploy/"
}

task npmLint(
        type: NpmTask,
        group: 'node',
        dependsOn: 'installWithCI',
        description: 'Run lint'
) {
    inputs.dir 'src/main'
    outputs.dir "$buildDir/lint-results"
    args = ['run', 'lint']
}

task npmBuild(
        type: NpmTask,
        group: 'node',
        dependsOn: 'npmLint',
        description: 'Build javascript'
) {
    inputs.dir 'src'
    outputs.dir "$buildDir/resources"
    args = ['run', 'build']
}

task packageApplication {
    dependsOn(':build')
    group 'application'
    description 'Packages entire application'
    doLast {
        copy {
            from "build/libs/${projectName}-${version}.jar"
            into "${deployDir}"
            rename ("${projectName}-${version}.jar", "${projectName}.jar")
        }
    }
}

task installWithCI(
        type: NpmTask,
        group: 'node',
        description: 'Install node dependencies with ci, ignoring scripts'
) {
    inputs.files "package.json"
    outputs.dir "./node_modules"
    args = ['ci', '--ignore-scripts', '--quiet']
}

task npm_install(
        type: NpmTask,
        group: 'node',
        description: 'Install node dependencies, ignoring scripts'
) {
    args = ['install', '--ignore-scripts', '--quiet']
}

task appName {
    group 'application'
    description 'Get the name of the application from gradle.properties'
    doLast {
        print "${projectName}".replaceAll(/[^a-zA-Z]/, "");
    }
}

task appVersion {
    group 'application'
    description 'Get the version of the application from gradle.properties'
    doLast {
        print "${version}"
    }
}

task enonicVersion {
    group 'application'
    description 'Get the enonic XP version from gradle.properties'
    doLast {
        print "${xpVersion}"
    }
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include 'com.github.tineikt:xp-lib-freemarker:1.1.6'
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo()
    maven { url 'https://jitpack.io' } // Needed by freemarker
}

wrapper {
    gradleVersion = '7.3.1'
    distributionType = Wrapper.DistributionType.BIN
}